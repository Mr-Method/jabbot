#!/usr/bin/env perl
use v5.18;
use FindBin;
use lib "$FindBin::Bin/../lib";

our $VERSION = "0.1";

use Jabbot;
use Jabbot::Core;

use Time::HiRes;

use Mojo::IOLoop;
use Mojolicious::Lite;

my $CORE = Jabbot::Core->new;

get '/' => sub {
    my $c = shift;
    $c->render(json => {
        name     => "jabber-core",
        version  => $VERSION,
        plugins  => [ map { ref($_) } @{ $CORE->{plugins} } ],
        services => $CORE->{services},
    });
};

get '/answers' => sub {
    my $c = shift;
    my $q = $c->req->json("/q") || $c->param('q');
    my %args = ( question => $q );
    my $begin_t = Time::HiRes::time;
    my $answers = $CORE->answers(%args);
    my $took = Time::HiRes::time - $begin_t;

    $c->render(json => {
        _took   => $took,
        answers => $answers
    });
};

post '/services' => sub {
    my $c = shift;

    my $srv = $c->req->json(); # { "name": "...", "host", "...", "port": "..." }

    my $begin_t = Time::HiRes::time;

    my $success = 0;
    if ( defined($srv->{name}) && defined($srv->{host}) && defined($srv->{port}) ) {
        $CORE->register_service($srv);
        $success = 1;
    }

    my $took = Time::HiRes::time - $begin_t;

    $c->render(json => {
        _took   => $took,
        success => $success
    });
};

my $app = app;
my $ioloop = Mojo::IOLoop->singleton;
$ioloop->recurring(
    15 => sub {
        $app->log->debug("Ping services");
        for (@{$CORE->{services}}) {
            my $uri = "http://" . $_->{host} . ":" . $_->{port};
            my $service_name = $_->{name};
            my $tx = Mojo::UserAgent->new->get($uri);
            if ($tx->success && $tx->res->json('/name') eq $service_name) {
                $app->log->debug("ALIVE: $service_name");
            } else {
                $app->log->debug("DEAD: $service_name");
                $CORE->unregister_service($service_name);
            }
        }
    }
);

$app->start;
