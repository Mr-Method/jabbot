#!/usr/bin/env perl
use v5.26;
use strict;
use utf8;

use Acme::RandomEmoji qw(random_emoji);
use Image::Randim::Source;

use FindBin;
use lib "$FindBin::Bin/../lib";
use Jabbot;
use Jabbot::Util qw(time_next_full_moon);
use Jabbot::External::TaiwanReservoir;

use Time::Moment;

use PlurkPoster;

sub random_image_url {
    my $source = Image::Randim::Source->new;
    $source->set_provider('Unsplash');
    my $image = $source->get_image;
    return $image->url;
}

sub next_full_moon_is_tonight {
    my $t_now = time;
    my $t_fullmoon = time_next_full_moon();

    my $t1 = Time::Moment->from_epoch($t_now)->at_midnight;
    my $t2 = Time::Moment->from_epoch($t_fullmoon)->at_midnight;

    return $t1 == $t2;
}

sub random_reservoir_condition {
    my $o = Jabbot::External::TaiwanReservoir->new;
    my $d = $o->usage_percentage;
    my @rows = sort { $a->{"UsagePercentage"} <=> $b->{"UsagePercentage"} } grep { $_->{"UsagePercentage"} && $_->{"ReservoirName"} =~ /水庫$/; } values %$d;
    my $picked = $rows[rand($#rows)];

    return sprintf(
        '現在 %s 的蓄水量是 %.2f%% 喔。',
        $picked->{"ReservoirName"},
        100*$picked->{"UsagePercentage"}
    );
}

sub post_to_plurk {
    my ($message) = @_;
    my $config = Jabbot->config->{plurk};
    my $plurk = PlurkPoster->new(
        username => $config->{username},
        password => $config->{password},
    );
    $plurk->login();
    $plurk->post($message);
}


## main

my $message = "";

$message = random_emoji(). " 隨機選圖\n" . random_image_url(). "\n來源： [Unsplash](https://unsplash.com)\n\n";

if (next_full_moon_is_tonight()) {
    $message .= "今晚是滿月呢。\n\n";
}

$message .= random_reservoir_condition() . "\n\n";

# say $message; exit();

post_to_plurk($message);

