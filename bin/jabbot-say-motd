#!/usr/bin/env perl
use v5.26;
use strict;
use warnings;
use utf8;

use Acme::RandomEmoji qw(random_emoji);
use Image::Randim::Source;

use FindBin;
use lib "$FindBin::Bin/../lib";
use Jabbot;
use Jabbot::Util qw(time_next_full_moon);
use Jabbot::External::TaiwanReservoir;
use Jabbot::External::FlickrPublicFeed;

use Time::Moment;

use PlurkPoster;

sub random_flickr_photo {
    my $photos = Jabbot::External::FlickrPublicFeed->photos;
    my $photo = $photos->[rand(@$photos)];
    return {
        preview    => $photo->{enclosure_url},
        source_url => $photo->{photo_page_url},
        source     => "flickr: $photo->{author}",
    };
}

sub random_unsplash_image {
    my $source = Image::Randim::Source->new;
    $source->set_provider('Unsplash');
    my $image = $source->get_image;
    return {
        preview => $image->url,
        source_url => "https://unsplash.com",
        source => "Unsplash",
    }
}

sub random_image_message {
    my $image = (rand > .5) ? random_flickr_photo() : random_unsplash_image();
    return "隨機選圖\n" . $image->{preview}. "\n來源： [$image->{source}]($image->{source_url})\n\n";
}

sub next_full_moon_is_tonight {
    my $t_now = time;
    my $t_fullmoon = time_next_full_moon();

    my $t1 = Time::Moment->from_epoch($t_now)->at_midnight;
    my $t2 = Time::Moment->from_epoch($t_fullmoon)->at_midnight;

    return $t1 == $t2;
}

sub random_reservoir_condition {
    my $o = Jabbot::External::TaiwanReservoir->new;
    my $d = $o->usage_percentage;
    my @rows = sort { $a->{"UsagePercentage"} <=> $b->{"UsagePercentage"} } grep { $_->{"UsagePercentage"} && $_->{"ReservoirName"} =~ /水庫$/; } values %$d;
    my $picked = $rows[rand($#rows)];

    return sprintf(
        '現在 %s 的蓄水量是 %.2f%% 喔。',
        $picked->{"ReservoirName"},
        100*$picked->{"UsagePercentage"}
    );
}

sub post_to_plurk {
    my ($message) = @_;
    my $config = Jabbot->config->{plurk};
    my $plurk = PlurkPoster->new(
        username => $config->{username},
        password => $config->{password},
    );
    $plurk->login();
    $plurk->post($message);
}


## main

my $message = random_image_message();

if (next_full_moon_is_tonight()) {
    $message .= "今晚是滿月呢。\n\n";
}

my $reservoir = eval { random_reservoir_condition() };
if ($reservoir) {
    $message .= $reservoir . "\n\n";
}

post_to_plurk($message);
